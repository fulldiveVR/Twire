apply plugin: "com.android.application"

// Load keystore properties
def keystorePropertiesFile = file("../../keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.embassource.cryptotwitch"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 508
        versionName "1.9.7"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        releaseConfig {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.releaseConfig
        }
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def nameSuff = variant.buildType.versionNameSuffix == null ? variant.versionName
                    : variant.versionName.replace(variant.buildType.versionNameSuffix, "")
            output.outputFileName = "CryptoTwitch-${variant.name}-${nameSuff}.apk"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.transition:transition:1.1.0"
    implementation "com.google.android.material:material:1.0.0"

    //https://github.com/bumptech/glide/releases
    implementation "com.github.bumptech.glide:glide:4.10.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.10.0"

    //https://github.com/square/picasso/blob/master/CHANGELOG.md
    implementation "com.squareup.picasso:picasso:2.5.2"

    //https://github.com/balysv/material-ripple/blob/master/CHANGELOG.md
    implementation "com.balysv:material-ripple:1.0.2"

    //https://github.com/rey5137/material/releases
    implementation "com.github.rey5137:material:1.3.0"

    //https://github.com/Perflyst/SRJNeeds
    implementation "com.github.perflyst:srjneeds:85306c8"

    //https://github.com/KasualBusiness/MaterialNumberPicker/releases
    implementation "biz.kasual:materialnumberpicker:1.2.1"

    //https://github.com/google/guava/releases
    implementation "com.google.guava:guava:28.1-jre"

    //https://github.com/google/gson/blob/master/CHANGELOG.md
    implementation "com.google.code.gson:gson:2.8.6"

    //https://github.com/JakeWharton/butterknife/blob/master/CHANGELOG.md
    implementation "com.jakewharton:butterknife:10.2.0"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.2.0"

    //https://github.com/ozodrukh/CircularReveal/releases
    implementation("com.github.ozodrukh:CircularReveal:1.3.1@aar") { transitive = true }

    //https://github.com/afollestad/material-dialogs/releases?after=2.0.0-alpha01
    implementation "com.github.afollestad.material-dialogs:core:0.8.6.0@aar"
    implementation "com.github.afollestad.material-dialogs:commons:0.8.6.0@aar"

    implementation "com.fulldive:eventsender:1.0.0"
    implementation ("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.50") {
        exclude group: "org.jetbrains", module: "annotations"
    }
    releaseImplementation project(":libtwitch")
}
